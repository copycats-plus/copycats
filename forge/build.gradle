plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
var commonVar = project(":common")
def generatedResources = commonVar.file("src/generated/resources")
loom {
    forge {
        mixinConfig "copycats-common.mixins.json"
        mixinConfig "copycats-forge.mixins.json"
    }

    runs {
        data {
            data()
            programArgs "--all", "--mod", "copycats"
            programArgs "--output", generatedResources.absolutePath
            programArgs "--existing", commonVar.file("src/main/resources").absolutePath
        }
    }

}
architectury {
    platformSetupLoomIde()
    forge()
}


configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    // Create and its dependencies
    modImplementation("com.simibubi.create:create-${rootProject.minecraft_version}:${rootProject.create_forge_version}:slim") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:${rootProject.registrate_forge_version}")
    modImplementation("com.jozufozu.flywheel:flywheel-forge-${rootProject.minecraft_version}:${rootProject.flywheel_forge_version}")

    modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_forge_version}")

    modCompileOnly("curse.maven:double-slabs-350179:3873442")

    compileOnly("io.github.llamalad7:mixinextras-common:${rootProject.mixin_extras_version}")
    annotationProcessor(implementation(include("io.github.llamalad7:mixinextras-forge:${rootProject.mixin_extras_version}")))

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
    archiveClassifier.set(null)
}

sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
